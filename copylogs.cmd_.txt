@echo off
setlocal
REM Script for copying logs, traces, events, etc for troubleshooting WU and Store issues.

REM 'Invisible' escape character
set ESC=

if "%~1"=="/?" (
    goto USAGE
)

REM Flush logs by stopping services before copying.
set _FLUSH_LOGS=1
REM Upgrade logs are rather huge, only copy if requested.
set _UPGRADE=0
REM Datastore doesn't compress well either.
set _DATASTORE=0
REM Collect from Xbox dev kit
set _XBOX=0

:ARGLOOP
if "%~1"=="" (
    goto START
)

if /I "%~1"=="/upgradelogs" (
    echo Will copy upgrade logs.
    set _UPGRADE=1
) else if /I "%~1"=="/datastore" (
    echo Will copy datastore.
    set _DATASTORE=1
) else if /I "%~1"=="/noflush" (
    echo Will not flush ETLs.
    set _FLUSH_LOGS=0
) else if /I "%~1"=="/xbox" (
    set _XBOX=1
)

shift
goto ARGLOOP

REM ---------------------------------------------------------------------------
:START

if "%_XBOX%" EQU "1" (
    REM For Xbox, check for connected state.
    xbconnect.exe /T >NUL 2>NUL
    if %ERRORLEVEL% NEQ 0 (
        echo Please xbconnect to Xbox first
        goto :EOF
    )
    goto :IS_ADMIN
)

REM Check for admin permission
REM If required tools don't exist, assume we're admin.
if not exist %windir%\system32\whoami.exe (
    goto IS_ADMIN
)

if not exist %windir%\system32\wscript.exe (
    goto IS_ADMIN
)

FOR /f "usebackq" %%f IN (`whoami /priv`) DO IF "%%f"=="SeTakeOwnershipPrivilege" GOTO :IS_ADMIN

REM Copy this file locally first, in case it's hosted on a share to avoid any
REM permission issues for the elevated user to access it.
copy /Y "%~dpfx0" "%TEMP%"
ECHO CreateObject("Shell.Application").ShellExecute Chr(34) ^& "%WINDIR%\System32\cmd.exe" ^& Chr(34), "/K " ^& Chr(34) ^& "%TEMP%\%~nx0 %*" ^& Chr(34), "", "runas", 1 >"%TEMP%\RunAs.vbs"
WScript.exe "%TEMP%\RunAs.vbs"
GOTO :EOF

REM ---------------------------------------------------------------------------
:IS_ADMIN

if "%_XBOX%" EQU "1" (
    REM xbrun /o reg query HKLM\SOFTWARE\Microsoft\WindowsUpdate\EditionSettings /v RootDirectory
    set _SOFTDIST=xs:\Deployment\SoftwareDistribution
    set _LOGSDIR=xs:\Deployment\SoftwareDistribution\logs
    set _DOHOME=xs:\Deployment\DeliveryOptimization
    set _COPY_CMD=xbcp.exe
    set _DIR_CMD=xbdir.exe
    set _NETSTOP_CMD=xbrun.exe /o /w net.exe stop
    REM _RUN_CMD is a command which will run code on the device. For non-Xbox, this should be blank.
    set _RUN_CMD=xbrun.exe /o /w
    set _ROBOCOPY_CMD=echo *** ERROR: Robocopy isn't supported on Xbox.
    set _ROBOCOPY_PARAMS=
) ELSE (
    set _SOFTDIST=%windir%\SoftwareDistribution
    set _LOGSDIR=%windir%\logs
    set _DOHOME=%windir%\ServiceProfiles\NetworkService\AppData\Local\Microsoft\Windows\DeliveryOptimization
    set _COPY_CMD=copy
    set _DIR_CMD=dir
    set _NETSTOP_CMD=net.exe stop
    set _RUN_CMD=
    set _ROBOCOPY_CMD=robocopy.exe
    set _ROBOCOPY_PARAMS=/W:1 /R:1 /NP /LOG+:%_TEMPDIR%\robocopy.log
)
if not exist %_SOFTDIST% (
    set _SOFTDIST=%programdata%\SoftwareDistribution
)

set _FOLDERNAME=WU_logs
set _SHARE=
set _SHAREDIR=%_SHARE%\Logs\%username%
set _TEMPDIR=%systemdrive%\%_FOLDERNAME%
set _CABNAME=WULogs-%USERNAME%-%COMPUTERNAME%
set _WUOLDETLPATH=%windir%.old\Windows\Logs\WindowsUpdate
set _OLDPROGRAMDATA=%windir%.old\ProgramData

mkdir %_TEMPDIR%
attrib -s -h %_TEMPDIR%

REM OS Version checks
for /f "skip=1 tokens=2 delims=[]" %%G in ('ver') Do (
    for /f "tokens=2,3,4 delims=. " %%x in ("%%G") Do (
        set _major=%%x& set _minor=%%y& set _build=%%z
    )
)

set _WIN8_OR_LATER=0
set _WINBLUE_OR_LATER=0
set _WIN10=0

if "%_XBOX%" NEQ "1" (
    echo OS Version: %_major%.%_minor%.%_build%

    IF %_major% GEQ 10 (
        set _WIN10=1
    ) ELSE IF %_major% GEQ 7 (
        set _WIN8_OR_LATER=1
        set _WINBLUE_OR_LATER=1
    ) ELSE IF %_major% == 6 (
        IF %_minor% GEQ 2 (
            set _WIN8_OR_LATER=1
        )
        IF %_minor% GEQ 3 (
            set _WINBLUE_OR_LATER=1
        )
    )
) ELSE (
    REM Assume Xbox is Win10
    set _WIN10=1
)

REM _HAS_POWERSHELL indicates if the machine running this script can run PowerShell.
set _HAS_POWERSHELL=0
REM Powershell is always in v1.0 dir.
IF EXIST %windir%\system32\WindowsPowerShell\v1.0\powershell.exe (
    set _HAS_POWERSHELL=1
)

ECHO Using temp directory: %_TEMPDIR%

call :COPYLOGS
call :COPY_FILE_VERSIONS
call :ZIP

REM Keep the console open in case user run the script from explorer.
pause

goto :EOF

REM ---------------------------------------------------------------------------
:COPYLOGS

REM DxDiag can be slow to execute, so copy it asynchronously with a best effort attempt. It's not important enough
REM to delay the whole script. Kick it off early so it has chance of finishing.
if "%_XBOX%" NEQ "1" (
    start dxdiag /t %_TEMPDIR%\DxDiag.txt
)

CALL :BANNER "Copying logs to temporary folder ..."
%_COPY_CMD% %_SOFTDIST%\ReportingEvents.log %_TEMPDIR%
%_COPY_CMD% %_SOFTDIST%\Plugins\7D5F3CBA-03DB-4BE5-B4B36DBED19A6833\TokenRetrieval.log %_TEMPDIR%
if "%_XBOX%" NEQ "1" (
    %_COPY_CMD% %windir%\windowsupdate.log %_TEMPDIR%
    %_COPY_CMD% %localappdata%\microsoft\windows\windowsupdate.log %_TEMPDIR%\WindowsUpdatePerUser.log
    %_COPY_CMD% "%windir%\windowsupdate (1).log" %_TEMPDIR%\Old.WindowsUpdate.log
    %_COPY_CMD% %systemdrive%\Windows.old\Windows\SoftwareDistribution\ReportingEvents.log %_TEMPDIR%\Old.ReportingEvents.log
)

if "%_XBOX%" NEQ "1" (
    REM -------------------------------------------------------------
    REM CBS & PNP logs
    %_ROBOCOPY_CMD% %_LOGSDIR%\cbs %_TEMPDIR% %_ROBOCOPY_PARAMS%
    %_COPY_CMD% %_LOGSDIR%\dpx\*.log %_TEMPDIR%
    %_COPY_CMD% %windir%\inf\*.log %_TEMPDIR%
    %_COPY_CMD% %windir%\winsxs\poqexec.log %_TEMPDIR%
    %_COPY_CMD% %windir%\winsxs\pending.xml %_TEMPDIR%
    %_COPY_CMD% %windir%\servicing\sessions\sessions.xml %_TEMPDIR%
    %_COPY_CMD% %_LOGSDIR%\MoSetup\UpdateAgent.log %_TEMPDIR%
)

if "%_XBOX%" NEQ "1" (
    REM UUP logs and action list xmls
    %_ROBOCOPY_CMD% %_SOFTDIST%\Download %_TEMPDIR%\UUP *.log *.xml %_ROBOCOPY_PARAMS%
)

if "%_XBOX%" NEQ "1" (
    REM -------------------------------------------------------------
    REM Windows Store logs.
    %_COPY_CMD% %temp%\winstore.log %_TEMPDIR%\winstore-Broker.log
    %_COPY_CMD% %userprofile%\AppData\Local\Packages\WinStore_cw5n1h2txyewy\AC\Temp\winstore.log %_TEMPDIR%
)

REM -------------------------------------------------------------
REM Copy datastore if requested.
IF "%_DATASTORE%"=="1" (
    echo.
    echo Copying WU Datastore ...
    %_NETSTOP_CMD% usosvc
    %_NETSTOP_CMD% wuauserv
    %_COPY_CMD% %_SOFTDIST%\datastore\DataStore.edb %_TEMPDIR%
)

REM -------------------------------------------------------------
REM WU ETLs for Win10+

echo.

IF %_WIN10% == 1 (
    echo.

    if "%_FLUSH_LOGS%"=="1" (
        echo Flushing WU ETL ...
        %_NETSTOP_CMD% usosvc
        %_NETSTOP_CMD% wuauserv
    )

    echo Copying WU ETLs ...
    %_COPY_CMD% %_LOGSDIR%\WindowsUpdate\*.etl %_TEMPDIR%
    if "%_XBOX%" NEQ "1" (
        %_ROBOCOPY_CMD% %ProgramData%\SoftwareDistribution\Logs\WindowsUpdate %_TEMPDIR% *.etl %_ROBOCOPY_PARAMS%
        %_ROBOCOPY_CMD% %SystemDrive%\ %_TEMPDIR% WindowsUpdateVerbose.etl %_ROBOCOPY_PARAMS%
    )
)

if "%_XBOX%" NEQ "1" (
    REM Also copy ETLs pre-upgrade
    IF EXIST %_WUOLDETLPATH% (
        %_ROBOCOPY_CMD% %_WUOLDETLPATH% %_TEMPDIR%\Windows.old\WU *.etl %_ROBOCOPY_PARAMS%
    )
)

if "%_XBOX%" NEQ "1" (
    REM -------------------------------------------------------------
    REM MUSE logs for Win10+
    sc query usosvc >NUL 2>&1
    IF NOT ERRORLEVEL 1 (
    echo.

    if "%_FLUSH_LOGS%"=="1" (
        echo Flushing USO ETL ...
        %_NETSTOP_CMD% usosvc
    )

    echo Copying MUSE logs ...
    mkdir %_TEMPDIR%\MUSE
    %_ROBOCOPY_CMD% %programdata%\UsoPrivate\UpdateStore %_TEMPDIR%\MUSE %_ROBOCOPY_PARAMS% /S
    %_ROBOCOPY_CMD% %programdata%\USOShared\Logs %_TEMPDIR%\MUSE %_ROBOCOPY_PARAMS% /S
    SCHTASKS /query /v /TN \Microsoft\Windows\UpdateOrchestrator\ > %_TEMPDIR%\MUSE\updatetaskschedules.txt

    %_ROBOCOPY_CMD% %_OLDPROGRAMDATA%\USOPrivate\UpdateStore %_TEMPDIR%\Windows.old\MUSE %_ROBOCOPY_PARAMS% /S
    %_ROBOCOPY_CMD% %_OLDPROGRAMDATA%\USOShared\Logs %_TEMPDIR%\Windows.old\MUSE %_ROBOCOPY_PARAMS% /S
    )
)

REM -------------------------------------------------------------
REM DO logs for Win10+
echo.

if "%_FLUSH_LOGS%"=="1" (
    echo Flushing DO ETL ...
    %_NETSTOP_CMD% usosvc
    %_NETSTOP_CMD% wuauserv
    %_NETSTOP_CMD% dosvc
)

echo Copying DO logs ...
mkdir %_TEMPDIR%\DO

%_COPY_CMD% %_LOGSDIR%\dosvc\*.etl %_TEMPDIR%\DO
if "%_XBOX%" NEQ "1" (
    %_COPY_CMD% %_SOFTDIST%\DeliveryOptimization\SavedLogs\*.log %_TEMPDIR%\DO
    %_COPY_CMD% %_SOFTDIST%\DeliveryOptimization\SavedLogs\*.etl %_TEMPDIR%\DO
    reg export HKEY_LOCAL_MACHINE\SOFTWARE\Microsoft\Windows\CurrentVersion\DeliveryOptimization %_TEMPDIR%\DO\registry_DO.txt
    reg export HKEY_LOCAL_MACHINE\SOFTWARE\Policies\Microsoft\Windows\DeliveryOptimization %_TEMPDIR%\DO\registry_DOpolicy.txt
)

REM DO logs location RS4
%_COPY_CMD% %_DOHOME%\Logs\*.etl %_TEMPDIR%\DO

REM DO state registry location RS4 onwards
mkdir %_TEMPDIR%\DO\State
%_COPY_CMD% %_DOHOME%\State\*.* %_TEMPDIR%\DO\State

if "%_XBOX%" NEQ "1" (
    REM -------------------------------------------------------------
    REM WU BVT logs.
    mkdir %_TEMPDIR%\BVT
    %_COPY_CMD% %systemdrive%\wubvt\*.log  %_TEMPDIR%\BVT\ >NUL 2>&1
    %_COPY_CMD% %systemdrive%\dcatebvt\*.log  %_TEMPDIR%\BVT\ >NUL 2>&1
    %_COPY_CMD% %systemdrive%\wuappxebvt\*.log  %_TEMPDIR%\BVT\ >NUL 2>&1
    %_COPY_CMD% %systemdrive%\wuuxebvt\*.log  %_TEMPDIR%\BVT\ >NUL 2>&1
    %_COPY_CMD% %systemdrive%\wuauebvt\*.log  %_TEMPDIR%\BVT\ >NUL 2>&1
    %_COPY_CMD% %systemdrive%\WUE2ETest\*.log  %_TEMPDIR%\BVT\ >NUL 2>&1
    %_COPY_CMD% %systemdrive%\taef\wubvt\*.log  %_TEMPDIR%\BVT\ >NUL 2>&1
    %_COPY_CMD% %systemdrive%\taef\wuappxebvt\*.log  %_TEMPDIR%\BVT\ >NUL 2>&1
    %_COPY_CMD% %systemdrive%\taef\wuuxebvt\*.log  %_TEMPDIR%\BVT\ >NUL 2>&1
    %_COPY_CMD% %systemdrive%\taef\wuauebvt\*.log  %_TEMPDIR%\BVT\ >NUL 2>&1
    %_COPY_CMD% %systemdrive%\taef\WUE2ETest\*.log  %_TEMPDIR%\BVT\ >NUL 2>&1
    %_COPY_CMD% %systemdrive%\taef\WUE2ETest\*.wtl  %_TEMPDIR%\BVT\ >NUL 2>&1
)

if "%_XBOX%" NEQ "1" (
    if "%_UPGRADE%"=="1" (
        call :BANNER "Copying upgrade logs"
        mkdir %_TEMPDIR%\UpgradeSetup
        %_COPY_CMD% %windir%\logs\mosetup\bluebox.log %_TEMPDIR%\UpgradeSetup\
        %_COPY_CMD% %systemdrive%\windows.old\windows\logs\mosetup\bluebox.log %_TEMPDIR%\UpgradeSetup\bluebox_windowsold.log
        %_COPY_CMD% %windir%\Panther\*.log %_TEMPDIR%\UpgradeSetup\
        %_COPY_CMD% %windir%\Panther\miglog.xml %_TEMPDIR%\UpgradeSetup\
        %_COPY_CMD% %systemdrive%\$Windows.~BT\Sources\Panther\setupact.log %_TEMPDIR%\UpgradeSetup\setupact_tildabt.log
        %_COPY_CMD% %systemdrive%\$Windows.~BT\Sources\Panther\setuperr.log %_TEMPDIR%\UpgradeSetup\setuperr_tildabt.log
        %_COPY_CMD% %systemdrive%\$Windows.~BT\Sources\Panther\miglog.xml %_TEMPDIR%\UpgradeSetup\miglog_tildabt.xml
    )
)

call :BANNER "Copying token cache and license store to temporary folder ..."
if "%_XBOX%" NEQ "1" (
    %_COPY_CMD% %windir%\ServiceProfiles\LocalService\AppData\Local\Microsoft\WSLicense\tokens.dat %_TEMPDIR%
)
%_COPY_CMD% %_SOFTDIST%\Plugins\7D5F3CBA-03DB-4BE5-B4B36DBED19A6833\117CAB2D-82B1-4B5A-A08C-4D62DBEE7782.cache %_TEMPDIR%

if "%_XBOX%" NEQ "1" (
    call :BANNER "Copying event logs to temporary folder ..."
    %_COPY_CMD% %windir%\System32\winevt\Logs\Microsoft-Windows-WindowsUpdateClient%%4Operational.evtx %_TEMPDIR%
    %_COPY_CMD% %windir%\System32\winevt\Logs\Microsoft-Windows-TaskScheduler%%4Operational.evtx %_TEMPDIR%
    %_COPY_CMD% %windir%\System32\winevt\Logs\Microsoft-Windows-Bits-Client%%4Operational.evtx %_TEMPDIR%
    %_COPY_CMD% %windir%\System32\winevt\Logs\Application.evtx %_TEMPDIR%
    %_COPY_CMD% %windir%\System32\winevt\Logs\System.evtx %_TEMPDIR%
    %_COPY_CMD% %windir%\System32\winevt\Logs\Setup.evtx %_TEMPDIR%
    %_COPY_CMD% %windir%\System32\Winevt\Logs\*AppX*.evtx %_TEMPDIR%
    %_COPY_CMD% %windir%\System32\Winevt\Logs\Microsoft-WS-Licensing%%4Admin.evtx  %_TEMPDIR%
    %_COPY_CMD% %windir%\System32\winevt\Logs\Microsoft-Windows-Kernel-PnP%%4Configuration.evtx %_TEMPDIR%
    %_COPY_CMD% %windir%\System32\winevt\Logs\Microsoft-Windows-Store%%4Operational.evtx %_TEMPDIR%
    %_COPY_CMD% %windir%\System32\winevt\Logs\Microsoft-Windows-DeliveryOptimization%%4Operational.evtx %_TEMPDIR%
)

if "%_XBOX%" NEQ "1" (
    call :BANNER "Logging registry to temporary folder ..."
    reg export HKEY_LOCAL_MACHINE\SOFTWARE\Microsoft\Windows\CurrentVersion\WindowsUpdate %_TEMPDIR%\registry_wu.txt
    reg export HKEY_LOCAL_MACHINE\SOFTWARE\Policies\Microsoft\Windows\WindowsUpdate %_TEMPDIR%\registry_wupolicy.txt
    reg export HKEY_LOCAL_MACHINE\SYSTEM\CurrentControlSet\Control\MUI\UILanguages %_TEMPDIR%\registry_langpack.txt
    reg export HKEY_LOCAL_MACHINE\Software\Policies\Microsoft\WindowsStore %_TEMPDIR%\registry_StorePolicy.txt
    reg export HKEY_LOCAL_MACHINE\Software\Microsoft\Windows\CurrentVersion\WindowsStore\WindowsUpdate %_TEMPDIR%\registry_StoreWUApproval.txt
    reg export HKEY_LOCAL_MACHINE\SYSTEM\CurrentControlSet\Services %_TEMPDIR%\registry_services.txt
    reg query "HKEY_LOCAL_MACHINE\SOFTWARE\Microsoft\Windows NT\CurrentVersion" /v BuildLab > %_TEMPDIR%\BuildInfo.txt
    reg query "HKEY_LOCAL_MACHINE\SOFTWARE\Microsoft\Windows NT\CurrentVersion" /v BuildLabEx >> %_TEMPDIR%\BuildInfo.txt
    reg query "HKEY_LOCAL_MACHINE\SOFTWARE\Microsoft\Windows NT\CurrentVersion" /v UBR >> %_TEMPDIR%\BuildInfo.txt
    reg query "HKEY_LOCAL_MACHINE\SOFTWARE\Microsoft\Windows NT\CurrentVersion" /v ProductName >> %_TEMPDIR%\BuildInfo.txt
    reg query HKEY_LOCAL_MACHINE\SOFTWARE\Microsoft\Windows\CurrentVersion\AppModel /v Version > %_TEMPDIR%\AppModelVersion.txt
    reg export HKEY_LOCAL_MACHINE\SYSTEM\CurrentControlSet\Control\FirmwareResources %_TEMPDIR%\registry_FirmwareResources.txt
    reg export HKEY_LOCAL_MACHINE\SOFTWARE\Microsoft\WindowsSelfhost %_TEMPDIR%\registry_WindowsSelfhost.txt
    reg export HKEY_LOCAL_MACHINE\SOFTWARE\Microsoft\WindowsUpdate %_TEMPDIR%\registry_wuhandlers.txt
    reg export HKEY_LOCAL_MACHINE\SOFTWARE\Microsoft\Windows\CurrentVersion\Appx %_TEMPDIR%\registry_appx.txt
    reg export "HKEY_LOCAL_MACHINE\SOFTWARE\Microsoft\Windows NT\CurrentVersion\Superfetch" %_TEMPDIR%\registry_superfetch.txt
    reg export "HKEY_LOCAL_MACHINE\SOFTWARE\Microsoft\Windows NT\CurrentVersion\Update\TargetingInfo" %_TEMPDIR%\registry_productdb.txt /y
)

CALL :BANNER "Getting networking configs ..."
%_RUN_CMD% ipconfig.exe /all > %_TEMPDIR%\ipconfig.txt
if "%_XBOX%" NEQ "1" (
    netsh winhttp show proxy > %_TEMPDIR%\winhttp.txt
    %_COPY_CMD% %windir%\System32\drivers\etc\hosts %_TEMPDIR%\hosts.txt
)

echo Getting gpresult ...
gpresult /H %_TEMPDIR%\gpresult.html

echo Getting msinfo32 ...
msinfo32 /nfo %_TEMPDIR%\sysinfo.nfo 

echo Getting directory lists ...
%_DIR_CMD% %_SOFTDIST% /s > %_TEMPDIR%\dir_softwaredistribution.txt
%_DIR_CMD% %_SOFTDIST% /ah /s > %_TEMPDIR%\dir_softwaredistribution_hidden.txt

echo Getting app list ...
if "%_XBOX%" NEQ "1" (
    if %_HAS_POWERSHELL% == 1 (
        if "%_WIN8_OR_LATER%"=="1" (
            powershell -command "import-module appx;get-appxpackage -allusers" > %_TEMPDIR%\GetAppxPackage.log
        )

        if "%_WINBLUE_OR_LATER%"=="1" (
            powershell -command "get-appxpackage -packagetype bundle" > %_TEMPDIR%\GetAppxPackageBundle.log
        )
    )
) else (
    xbapp.exe list > %_TEMPDIR%\GetAppxPackage.log
)

if "%_XBOX%" NEQ "1" (
    echo Getting download list ...
    bitsadmin /list /allusers /verbose > %_TEMPDIR%\bitsadmin.log
)

if "%_XBOX%" NEQ "1" (
    echo Getting certificate list ...
    certutil -store root > %_TEMPDIR%\certs.txt 2>&1
)

if "%_XBOX%" NEQ "1" (
    echo Getting installed update list ...
    wmic qfe get hotfixid > %_TEMPDIR%\InstalledUpdates.log 2>&1
)

echo Getting DO configuration ...
powershell -command "Get-DOConfig -verbose" > %_TEMPDIR%\DOConfig.txt

%_RUN_CMD% sc.exe query wuauserv > %_TEMPDIR%\wuauserv-state.log
%_RUN_CMD% schtasks.exe /query /v /TN \Microsoft\Windows\WindowsUpdate\ > %_TEMPDIR%\WUScheduledTasks.log

goto :EOF


REM ---------------------------------------------------------------------------
:COPY_FILE_VERSIONS

if "%_XBOX%" EQU "1" (
    REM Not implemented for xbox.
    goto :EOF
)

if %_HAS_POWERSHELL% == 0 (
    goto :EOF
)

call :BANNER "Collecting file versions ..."
set _VERSION_SCRIPT=%TEMP%\WUFileVersion.ps1

echo # WUFileVersion.ps1 > %_VERSION_SCRIPT%

echo $binaries = @("wuaext.dll", "wuapi.dll", "wuaueng.dll", "wucltux.dll", "wudriver.dll", "wups.dll", "wups2.dll", "wusettingsprovider.dll", "wushareduxresources.dll", "wuwebv.dll", "wuapp.exe", "wuauclt.exe", "storewuauth.dll", "wuuhext.dll", "wuuhmobile.dll", "wuau.dll", "wuautoappupdate.dll") >> %_VERSION_SCRIPT%

echo foreach($file in $binaries) >> %_VERSION_SCRIPT%
echo { >> %_VERSION_SCRIPT%
echo     if(test-path "$env:windir\system32\$file")  >> %_VERSION_SCRIPT%
echo     {  >> %_VERSION_SCRIPT%
echo        $version = (Get-Command "$env:windir\system32\$file").FileVersionInfo >> %_VERSION_SCRIPT%
echo        write-host "$file : $($version.FileMajorPart).$($version.FileMinorPart).$($version.FileBuildPart).$($version.FilePrivatePart)" >> %_VERSION_SCRIPT%
echo     }  >> %_VERSION_SCRIPT%
echo } >> %_VERSION_SCRIPT%

echo $muis = @("wuapi.dll.mui", "wuaueng.dll.mui", "wucltux.dll.mui", "wusettingsprovider.dll.mui", "wushareduxresources.dll.mui") >> %_VERSION_SCRIPT%

echo foreach($file in $muis) >> %_VERSION_SCRIPT%
echo { >> %_VERSION_SCRIPT%
echo     if(test-path "$env:windir\system32\en-US\$file")  >> %_VERSION_SCRIPT%
echo     {  >> %_VERSION_SCRIPT%
echo        $version = (Get-Command "$env:windir\system32\en-US\$file").FileVersionInfo >> %_VERSION_SCRIPT%
echo        write-host "$file : $($version.FileMajorPart).$($version.FileMinorPart).$($version.FileBuildPart).$($version.FilePrivatePart)" >> %_VERSION_SCRIPT%
echo     }  >> %_VERSION_SCRIPT%
echo } >> %_VERSION_SCRIPT%

powershell -ExecutionPolicy Bypass -Command %_VERSION_SCRIPT% > %_TEMPDIR%\FileVersions.log
del %_VERSION_SCRIPT%

goto :EOF

REM ---------------------------------------------------------------------------
:ZIP

if %_HAS_POWERSHELL% == 0 (
    call :COPY_UNCOMPRESSED
    goto :EOF
)

if not exist %windir%\system32\shell32.dll (
    call :COPY_UNCOMPRESSED
    goto :EOF
)

call :BANNER "Zipping logs ..."

set sourceFolder=%_TEMPDIR%
set time2=%time: =0%
set dateTimelog=%date:~0,4%%date:~5,2%%date:~8,2%%time2:~0,2%%time2:~3,2%%time2:~6,2%
set zipFileName=%_CABNAME%-%dateTimelog%.zip
set zipFilePath=C:\%zipFileName%

set _PSVER=
for /f "delims=" %%f in ('powershell -Command "$PSVersionTable.PSVersion.Major"') do (
  set _PSVER=%%f
)
echo Found powershell v%_PSVER%.
echo.
if %_PSVER% GEQ 5 (
    REM This version requires powershell 5+
    powershell.exe -ExecutionPolicy Bypass -Command "Compress-Archive -LiteralPath '%sourceFolder%' -DestinationPath '%zipFilePath%'"
) else (
    CALL :ZIPLOGS_ALT "%sourceFolder%" "%zipFilePath%"
)

if not exist "%zipFilePath%" (
    call :COPY_UNCOMPRESSED
) else (
    echo Logs zipped to %zipFilePath%

    call :BANNER "Uploading logs ..."

    net use %_SHARE% >NUL 2>NUL
    if not exist "%_SHARE%" (
        echo ˆÈ‰º‚Ìƒtƒ@ƒCƒ‹‚É‚Â‚¢‚Ä•¾ŽÐˆ¶‚É‚¨‘—‚è‚­‚¾‚³‚¢‚Ü‚·‚æ‚¤‚¨Šè‚¢‚¢‚½‚µ‚Ü‚·B:
        echo %zipFilePath%
	rd /s /q %_TEMPDIR%
        goto :EOF
    )

    if not exist "%_SHAREDIR%" ( md %_SHAREDIR% )
    copy "%zipFilePath%" "%_SHAREDIR%\%zipFileName%"
    if errorlevel 1 (
        echo.
        echo ˆÈ‰º‚Ìƒtƒ@ƒCƒ‹‚É‚Â‚¢‚Ä•¾ŽÐˆ¶‚É‚¨‘—‚è‚­‚¾‚³‚¢‚Ü‚·‚æ‚¤‚¨Šè‚¢‚¢‚½‚µ‚Ü‚·B:
        echo %zipFilePath%
	rd /s /q %_TEMPDIR%
        goto :EOF
    )

    echo.
    echo Logs uploaded. Please include this in your email:
    echo %_SHAREDIR%\%zipFileName%
)
goto :EOF

:COPY_UNCOMPRESSED
echo.
echo Unable to zip from powershell. Copying files directly.
echo.

REM Log the username so we can remember who it's from.
echo %COMPUTERNAME% %USERNAME% > %_TEMPDIR%\MACHINENAME_%COMPUTERNAME%_USERNAME_%USERNAME%.txt

setlocal ENABLEDELAYEDEXPANSION

for /f "tokens=3" %%G in ( 'reg query HKEY_LOCAL_MACHINE\SOFTWARE\Microsoft\Windows\CurrentVersion\WindowsUpdate /v SusClientId' ) do ( set _SUSCLIENTID=%%G )

REM Sometimes susclientid reg value may be missing. Replace with username in that case.
if "%_SUSCLIENTID%" == "" (
  set _SUSCLIENTID=%USERNAME%
)

set _SHAREDIR=%_SHARE%\Logs\%_SUSCLIENTID%

net use %_SHARE%
%_ROBOCOPY_CMD% %_TEMPDIR% %_SHAREDIR% /E

if errorlevel 16 (
  echo.
  echo **********************************************************************************
  echo Can't access %_SHAREDIR%. Please manually copy %_TEMPDIR% to a share.
) else (
  echo.
  echo **********************************************************************************
  echo Done. Logs copied to %_SHAREDIR%
)
echo **********************************************************************************

goto :EOF

:USAGE
echo.
echo CopyLogs.cmd [/datastore] [/upgradelogs] [/noflush] [/xbox]
echo.
echo Collect Windows Update and Windows Store troubleshooting data.
echo.
echo Options:
echo    /datastore      Also copy Windows Update datastore file.
echo    /upgradelogs    Also copy Windows Setup upgrade logs.
echo    /noflush        Do not flush log files to avoid stopping Windows Update related services.
echo    /xbox           Copy logs from an Xbox development kit.
echo.
goto :EOF

:ZIPLOGS_ALT
setlocal
REM Use Shell CopyHere verb.
set _SOURCEFOLDER=%~1
set _ZIPFILEPATH=%~2

REM CREDIT: The zipping method was grabbed from Sean Kearney @energizedtech (www.powershell.ca)
REM http://www.energizedtech.com/2010/06/powershell-send-files-to-a-com.html

set _ZIP_SCRIPT=%TEMP%\WUZipFile.ps1

echo # WUZipFile.ps1 > %_ZIP_SCRIPT%

echo $zipHeader=[char]80 + [char]75 + [char]5 + [char]6 + [char]0 + [char]0 + [char]0 + [char]0 + [char]0 + [char]0 + [char]0 + [char]0 + [char]0 + [char]0 + [char]0 + [char]0 + [char]0 + [char]0 + [char]0 + [char]0 + [char]0 + [char]0 >> %_ZIP_SCRIPT%

echo $sourceFolder = "%_SOURCEFOLDER%" >> %_ZIP_SCRIPT%
echo $folderSize = Get-ChildItem $sourceFolder ^| Measure-Object -Property length -sum >> %_ZIP_SCRIPT%
echo $zipFilePath = "%_ZIPFILEPATH%" >> %_ZIP_SCRIPT%

echo Add-Content $zipFilePath -value $zipHeader >> %_ZIP_SCRIPT%
echo $explorerShell = New-Object -com 'Shell.Application' >> %_ZIP_SCRIPT%
echo $sendToZip = $explorerShell.Namespace($zipFilePath.ToString()).CopyHere($sourceFolder.ToString()) >> %_ZIP_SCRIPT%

REM When zipping is in progress, the cab size is always 24 bytes. However, if folder size is really big, size will go beyond 24 bytes even when zipping is still
REM in progress. Since there's no way to track progress, we're going to wait until we hit the expected size. Explorer's compression ratio is about 0.05.
REM We'll be conservative and wait until either we reach that size or timeout after 60 secs.
echo $size = $folderSize.Sum * 0.05 >> %_ZIP_SCRIPT%
echo $timeout = 60 >> %_ZIP_SCRIPT%
echo $progress = "." >> %_ZIP_SCRIPT%
echo do >> %_ZIP_SCRIPT%
echo { >> %_ZIP_SCRIPT%
echo     $item = Get-Item $zipFilePath >> %_ZIP_SCRIPT%
echo     write-host $progress >> %_ZIP_SCRIPT%
echo     Start-Sleep 1 >> %_ZIP_SCRIPT%
echo     $progress = $progress + "." >> %_ZIP_SCRIPT%
echo     $timeout = $timeout - 1 >> %_ZIP_SCRIPT%
echo } while ($item.Length -lt $size -and $timeout -gt 0) >> %_ZIP_SCRIPT%

type %_ZIP_SCRIPT%
pause

powershell -ExecutionPolicy Bypass -Command %_ZIP_SCRIPT%
del %_ZIP_SCRIPT%
endlocal
goto :EOF

:BANNER
echo.
echo %ESC%[1;33mSTATUS: %ESC%[0m%ESC%[40;1m%~1%ESC%[0m
echo.
goto :eof

